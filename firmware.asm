;************************************************************************************
;							TRABALHO MOSTRA TECNOLOGICA ULTRAMIG 2017
;************************************************************************************
;AUTORES: FABRICIO GOMES, LUCAS VENTURA , MARIA DO CARMO
;PROJETO: CONTROLE DE ACESSO
; MODULO CONTROLE DE ACESSO POR RFID E SENHA DE 4 DIGITOS COM COMUNICAÇÃO E MONIMORAMENTO
;
;
;************************************************************************************
;							CONFIGURACOES PARA GRAVACAO - FUSES
;************************************************************************************
 __CONFIG _CP_OFF&_CPD_OFF&_DEBUG_OFF&_LVP_OFF&_WRT_OFF&_BODEN_OFF&_PWRTE_ON&_WDT_OFF&_HS_OSC

#INCLUDE <P16F877A.INC> 			;ARQUIVO PADRÃO MICROCHIP PARA 16F877A
 ;
#DEFINE BANCO0 BCF STATUS,RP0 		;SELECIONA BANCO0 
#DEFINE BANCO1 BSF STATUS,RP0 		;SELECIONA BANCO1
;************************************************************************************
;								DEFINICAO DAS VARIAVEIS
;************************************************************************************
 CBLOCK 0x20 						;ENDEREÇO INICIAL DOS REGISTRADORES DE USO GERAL
 ; TEMPO
 TEMPO1,TEMPO2,TEMPO3
 ; LINHA  LCD 
 LCDLINHA1,LCDLINHA2
 ; RFID
 RF0,RF1,RF2,RF3,RF4,RF5,RF6,RF7,RF8,RF9
 ERF0,ERF1,ERF2,ERF3,ERF4,ERF5,ERF6,ERF7,ERF8,ERF9
 ; DIGITOS DE SENHA
 S0,S1,S2,S3,ES0,ES1,ES2,ES3
 ; CONTROLE DE USUARIO
 USU 
 ;AUXILIARES GERAIS
 ADDRESS,TMP,AUX,AUX1,AUXTC,AUX1TC
 LOOPTEMPO,CONTBEEP,CABERTO,RXUART 
 AUXNIVELSEG,ALARME
 AUXDIG;0= CONTROLE DE * TELA
  	   ;1= CONTROLE DE MSG RESULTADO COMPARAÇÃO DE SENHA
 	   ;2= CONTROLE DE DIGITAÇÃO DE NOVA SENHA
       ;3= CONTROLE DE LEITURA RFID
 	   ;4= NIVEL DE SEGURANÇA
	   ;5= FLAG NIVEL SEGURANÇA
	   ;6= FLAG SENHA OK
	   ;7= FLAG RFID OK
 ENDC ;FIM DO BLOCO DE MEMÓRIA
 ; 
 ; DEFINIÇÃO DE VARIAVEIS DE I/O
 #DEFINE DISPLAY 			PORTD
 #DEFINE ENABLE				PORTE,0
 #DEFINE RS					PORTE,1
 #DEFINE LED				PORTE,2
 #DEFINE PORTAO				PORTC,0
 #DEFINE SENSOR				PORTC,1
 #DEFINE BUZZER 			PORTC,2
 #DEFINE GRVRFID			PORTC,3
 ;
 ; PAGINAÇÃO DE MEMORIA
 ORG H'2100' 
 DE   0X31,0X32,0X33,0X34,0X00
 ; REGISTRADOR DE INICIO
 ORG 0X0000
 CLRF ALARME
 GOTO INICIO
 ;
 ORG 0X0004 ;ENDERECO DO VETOR DE INTERRUPCAO
 MOVLW .10
 CALL DELAY3
 ;BTFSC PIR1,RCIF    ;VERIFICA SE POSSUI DADOS NA SERIAL(DESATIVADO DEVIDO INTERFERENCIA NO RETORNO LOOP)
 ;GOTO RECEBEUART 
 BSF LED
 MOVLW .200
 CALL DELAY3 
 BCF LED
 GOTO VTECLADO
 ;
 ; TABELA DE DADOS COM MENSAGENS
MENSAGEM
  MOVWF    PCL
MSG1:
 DT " UTRAMIG - 2017 ",0
MSG2:
 DT "APROXIME A TAG  ",0
MSG3:
 DT "SENHA INCORRETA ",0
MSG4:
 DT " SENHA  CORRETA ",0
MSG5:
 DT "                ",0   ;APAGAR LINHA INTEIRA
MSG7:
 DT "DIG. NOVA SENHA",0
MSG8:
 DT "GRAV. C/ SUCESSO",0
MSG9:
 DT "TAG N CADASTRADA",0
MSG10:
 DT " ACESSO NEGADO",0
MSG11:
 DT "ACESSO LIBERADO",0
MSG12:
 DT " PORTAO  ABERTO",0
MSG13:
 DT "     TAG OK",0
MSG14:
 DT "DIG. SENHA",0
MSG15:
 DT "DIG. NIVEL SEG",0
 RETURN
 ;
 ; TABELA DE DIGITOS DO TECLADO
TABTECLADO	
 ADDWF PCL,F
 RETLW   0X31	    			;0-0 = 1
 RETLW   0X34				    ;1-0 = 4
 RETLW   0X37				    ;2-0 = 7
 RETLW   0X2A				    ;3-0 = *
 RETLW   0X32				    ;0-1 = 2
 RETLW   0X35				    ;1-1 = 5
 RETLW   0X38				    ;2-1 = 8
 RETLW   0X30				    ;3-1 = 0
 RETLW   0X33				    ;0-2 = 3
 RETLW   0X36				    ;1-2 = 6
 RETLW   0X39				    ;2-2 = 9
 RETLW   0X23					;3-2 = #
 RETLW   0X41				    ;0-3 = A
 RETLW   0X42				    ;1-3 = B
 RETLW   0X43				    ;2-3 = C
 RETLW   0X44				    ;3-3 = D
 RETURN
 ; 
 ; INCIO DO PROGRAMA
INICIO
 CALL CONFIGURACAO
 MOVF ALARME,W				; GUARDA VARIAVEL DE ALARME ANTES DE ZERO DE VARIAVEIS
 CALL ZERAVARIAVEIS			; LIMPA VARIAVEIS 
 MOVFW ALARME				; RECUPERA VARIAVEL DE ALARME
 BTFSC ALARME,0
 GOTO PORTAOABERTO3A		; DESVIO CASO O ALARME ESTEJA ACIONADO 
 CALL LEITURASENHA			; REALIZA LEITURA DA SENHA GRAVADA NA EEPROM
 CALL LEITURARFID			; REALIZA LEITRA DA TAG RFID GRAVADA NA EEPROM
 BCF AUXDIG,0				; LIMPA BITS DE CONTROLE DE DIGITAÇÃO (DETALHES DA DECLARAÇÃO DA VARIAVEL)
 BCF AUXDIG,1
 BCF AUXDIG,2
 BCF AUXDIG,3
 ;
MAIN
 ;MENSAGEM INICIAL
 MOVLW .80 
 CALL DELAY3
 MOVLW MSG1
 MOVWF LCDLINHA1
 MOVLW MSG5
 MOVWF LCDLINHA2
 CALL LCD 
 ; 
; /////////////////////////////PROGRAMA LOOP//////////////////////////////////////////
LOOP
 BTFSS SENSOR
 CALL PORTAOABERTO3
 BTFSC AUXDIG,0			;VERIFICA SE O USUARIO ESTÁ UTILIZANDO O TECLADO
 GOTO LOOP
 BTFSC PIR1,RCIF
 GOTO RECEBEUART		; DESVIA PARA LOOP DE VERIFICAÇÃO DE TAG RFID
 GOTO LOOP
;////////////////////////////////////////////////////////////////////////////////////
;***********************************************************************************
;***********************************************************************************
;
;
;                  SUBROTINAS
;
;
;***********************************************************************************
;***********************************************************************************
;***********************************************************************************
;ESCRITA LCD
;***********************************************************************************
LCD
 CALL INICIALIZALCD ;ROTINA DE CONFIGURACAO DO DISPLAY
 BCF RS 			;SELECIONA O DISPLAY PARA COMANDOS
 MOVLW 0X01
 CALL ESCREVE 		;ESCREVE COMANDO PARA LIMPAR A TELA
 MOVLW .2
 CALL DELAY3 		;DELAY DE 1ms
 MOVLW 0X80 		;COMANDO P/POSICIONAR O CURSOR LINHA 0/COLUNA 6
 CALL ESCREVE
 BSF RS
 MOVFW LCDLINHA1
 MOVWF  AUX
LCD1
 CALL MENSAGEM
 ADDLW  .0
 BTFSC  STATUS,Z
 GOTO  LCD2
 CALL ESCREVE
 INCF   AUX,F
 MOVFW   AUX
 GOTO  LCD1
 ;
LCD2 
 BCF RS 			;SELECIONA O DISPLAY PARA COMANDOS
 MOVLW .200
 CALL DELAY3		;DELAY DE 0.1s
 MOVLW 0XC0 		;COMANDO P/POSICIONAR O CURSOR LINHA 1/COLUNA 2
 CALL ESCREVE
 BSF RS
 MOVFW LCDLINHA2	;CARREGA DIGITO DA PRIMERA LINHA NO W
 MOVWF  AUX			
LCD3
 CALL MENSAGEM		;BUSCA NOVO DIGITO PARA IMPRESSAO NO LCD
 ADDLW  .0			
 BTFSC  STATUS,Z	; VERIFICA FINAL DE MENSAGEM	
 GOTO  LCD4
 CALL ESCREVE		; REALIZA A IMPRESSAO DO DIGITO NO LCD
 INCF   AUX,F
 MOVFW   AUX
 GOTO  LCD3
LCD4  
 BCF RS 			;SELECIONA O DISPLAY PARA COMANDOS
 MOVLW .200
 CALL DELAY3 		;CHAMA SUBROTINA DE DELAY 2 
 RETURN
;***********************************************************************************
;ESCRITA LCD
;***********************************************************************************
;***********************************************************************************
;SUBROTINA DELAY
;***********************************************************************************
; SUBROTINA QUE GERA UMA BASE DE TEMPO NO W
DELAY:
 CALL DELAY1
 DECFSZ LOOPTEMPO
 GOTO DELAY
 RETURN
 ;
DELAY1A 
 MOVLW D'250'
 MOVWF TEMPO1
 ;
DELAY2A
 MOVLW D'200'
 MOVWF TEMPO2	
DELAY3A
 NOP
 NOP
 NOP
 NOP
 NOP
 NOP
 NOP
 DECFSZ TEMPO2
 GOTO DELAY3A
 ;
 DECFSZ TEMPO1
 GOTO DELAY2A
 RETURN
;***********************************************************************************
;SUBROTINA DELAY
;***********************************************************************************
;***********************************************************************************
;SUBROTINA DELAY1
;***********************************************************************************
; SUBROTINA QUE GERA UMA BASE DE TEMPO DE NO MINIMO 1ms
DELAY1
 MOVWF TEMPO2
DL2
 MOVLW .200
 MOVWF TEMPO3
DL1
 NOP 
 NOP 
 DECFSZ TEMPO3,F 
 GOTO DL1 
 DECFSZ TEMPO2,F
 GOTO DL2
 RETURN 
;***********************************************************************************
; SUBROTINA DELAY1
;***********************************************************************************
;***********************************************************************************
;SUBROTINA DELAY2
;***********************************************************************************
; SUBROTINA QUE GERA UMA BASE DE TEMPO DE NO MINIMO 100ms
DELAY3
 MOVWF TEMPO2
DL222
 MOVLW .250
 MOVWF TEMPO3
DL111
 NOP 
 NOP 
 NOP 
 NOP 
 NOP 
 NOP 
 NOP 
 DECFSZ TEMPO3,F 
 GOTO DL111 
 DECFSZ TEMPO2,F
 GOTO DL222
 RETURN 
;***********************************************************************************
;SUBROTINA DELAY2
;***********************************************************************************
;
;************************************************************************************
;SUBROTINA DISPLAY
;************************************************************************************
; ROTINA ENVIA UM CARACTER PARA O LCD CARREGADA NO REG. W
;
ESCREVE
 MOVWF DISPLAY 	;ATUALIZA DISPLAY
 MOVLW .1
 CALL DELAY1
 BSF ENABLE 	;ENVIA UM PULSO DE HABILITACAO (ENABLE) PARA O DISPLAY
 GOTO $+1
 BCF ENABLE
 MOVLW .6
 CALL DELAY3 	;DELAY DE 3ms
 RETURN 
;************************************************************************************
;INICIALIZA O LCD
;************************************************************************************
; PARA 2 LINHAS, CURSOR APAGADO E DESLOCAMENTO DO CURSOR A DIREITA
INICIALIZALCD
 BCF RS 			;SELECIONA O DISPLAY PARA COMANDOS
 MOVLW 0X30 		;ESCREVE COMANDO 0X3O PARA INICIALIZACAO
 CALL ESCREVE
 MOVLW .6 			;DELAY DE 3MS (EXIGIDO PELO DISPLAY)
 CALL DELAY3
 MOVLW 0X30 		;ESCREVE COMANDO 0X3O PARA INICIALIZACAO
 CALL ESCREVE
 MOVLW 0X30
 CALL ESCREVE
 MOVLW B'00111000'  ;ESCREVE COMANDO PARA INTERFACE D E 8 VAIS DE DADOS
 CALL ESCREVE
 MOVLW B'00000001'
 CALL ESCREVE 		;ESCREVE COMANDO PARA LIMPAR TODO O DISPLAY
 MOVLW .2
 CALL DELAY3 		;DELAY DE 1MS
 MOVLW B'00001100'
 CALL ESCREVE 		;ESCREVE COMANDO PARA LIGAR O DISPLAY SEM CURSOR
 MOVLW B'00000110'
 CALL ESCREVE 		;ESCREVE COMANDO PARA INCREMENTAR AUTOMATICA A DIREITA
 BSF RS 			;SELECIONA O DISPLAY PARA DADOS
 RETURN 
;************************************************************************************
;SUBROTINA DISPLAY
;************************************************************************************
;
;************************************************************************************
;SUBROTINA COMUNICAÇÃO SERIAL
;************************************************************************************
;***********************************************************************************
;LEITURA NA SERIAL UART
;***********************************************************************************
RECEBEUART
 BTFSS SENSOR
 GOTO PORTAOABERTO3
 BTFSS PIR1,RCIF    ;VERIFICA SE POSSUI DADOS NA SERIAL
 GOTO RECEBEUART
 MOVF RCREG,W
 MOVWF RXUART
 ;
 BTFSC RCSTA,OERR
 GOTO RECEBEUART_ERRO 
 ;
 INCF RF0			;INCREMENTA 1 NA VARIAVEL PARA VERIFICA SE JÁ FOI UTILIZADA
 DECFSZ RF0			; CASO JÁ FOI UTILIZADA SALTA PARA VEIFICAÇÃO DE OUTRA VARIAVEL
 GOTO $+4
 MOVF RXUART,W		; CARREGA O VALOR DO DIGITO NO REGISTRADOR W
 MOVWF RF0			; CARRREGA O VALOR DO DIGITO NA VERIAVEL TEMPORARIA
 GOTO RECEBEUART		; RETORNA PARA VERIFICAÇÃO 
 ;
 INCF RF1
 DECFSZ RF1
 GOTO $+4
 MOVF RXUART,W
 MOVWF RF1
 GOTO RECEBEUART
 ;
 INCF RF2
 DECFSZ RF2
 GOTO $+4
 MOVF RXUART,W
 MOVWF RF2
 GOTO RECEBEUART
 ;
 INCF RF3
 DECFSZ RF3
 GOTO $+4
 MOVF RXUART,W
 MOVWF RF3
 GOTO RECEBEUART
 ;
 INCF RF4
 DECFSZ RF4
 GOTO $+4
 MOVF RXUART,W
 MOVWF RF4
 GOTO RECEBEUART
 ;
 INCF RF5
 DECFSZ RF5
 GOTO $+4
 MOVF RXUART,W
 MOVWF RF5
 GOTO RECEBEUART
 ;
 INCF RF6
 DECFSZ RF6
 GOTO $+4
 MOVF RXUART,W
 MOVWF RF6
 GOTO RECEBEUART
 ;
 INCF RF7
 DECFSZ RF7
 GOTO $+4
 MOVF RXUART,W
 MOVWF RF7
 GOTO RECEBEUART
 ;
 INCF RF8
 DECFSZ RF8
 GOTO $+4
 MOVF RXUART,W
 MOVWF RF8
 GOTO RECEBEUART
 ;
 MOVF RXUART,W
 MOVWF RF9
 BSF AUXDIG,3
 ;
 BSF BUZZER				; BIP DE DIGITAÇÃO
 BSF LED				; LED DE INDICAÇÃO
 MOVLW .200
 CALL DELAY3
 BTFSC GRVRFID
 GOTO GRAVARFID
 BCF LED
 BCF BUZZER
 ;
 ; NAO TESTAR O PRIMEIRO BYTE
 ;MOVFW ERF0
 ;XORWF RF0
 ;BTFSS STATUS,Z 
 ;GOTO MSGRFID
 ;
 ;MOVFW ERF1
 ;XORWF RF1
 ;BTFSS STATUS,Z 
 ;GOTO MSGRFID
 ;
 ;MOVFW ERF2
 ;XORWF RF2
 ;BTFSS STATUS,Z 
 ;GOTO MSGRFID 
 ;
 MOVFW ERF3
 SUBWF RF3
 BTFSS STATUS,Z 
 GOTO MSGRFID
 ;
 MOVFW ERF4
 SUBWF RF4
 BTFSS STATUS,Z 
 GOTO MSGRFID
 ;
 MOVFW ERF5
 SUBWF RF5
 BTFSS STATUS,Z 
 GOTO MSGRFID
 ;
 ;MOVFW ERF6
 ;XORWF RF6
 ;BTFSS STATUS,Z 
 ;GOTO MSGRFID
 ;
 ;MOVFW ERF7
 ;XORWF RF7
 ;BTFSS STATUS,Z 
 ;GOTO MSGRFID
 ;
 ;MOVFW ERF8
 ;XORWF RF8
 ;BTFSS STATUS,Z 
 ;GOTO MSGRFID
 ;
 ;NÃO TESTAR O ULTIMA BYTE
 ;MOVFW ERF9
 ;XORWF RF9
 ;BTFSS STATUS,Z 
 ;GOTO MSGRFID
 GOTO MSGRFID1
 ;
RECEBEUART_ERRO
 BCF RCSTA,4 		;RESET DE ERRO UART
 MOVLW .2
 CALL DELAY3 
 BSF RCSTA,4 
 GOTO RECEBEUART
;***********************************************************************************
;LEITURA NA SERIAL UART
;***********************************************************************************
;***********************************************************************************
;ESCREVE NA SERIAL UART (NÃO FOI NECESSARIO UTLIZAR NESSE PROJETO)
;***********************************************************************************
SEND
 MOVWF TXREG         ;MOVE PARA TXREG VALOR A SER ENVIADO
TRANSWT
 BANCO1
WTHERE
 BTFSS TXSTA,TRMT    ;TESTA SE JA ENVIOU O DADO
 GOTO WTHERE         ;SE NAO AGUARDA ENVIAR
 BANCO0
 RETURN
 ;
;***********************************************************************************
;ESCREVE NA SERIAL UART
;***********************************************************************************
;************************************************************************************
;SUBROTINA COMUNICAÇÃO SERIAL
;************************************************************************************
;
;************************************************************************************
;SUBROTINA EEPROM
;************************************************************************************
;***********************************************************************************
;LEITURA EEPROM
;***********************************************************************************
LEEPROM
 BANKSEL EEADR			; COMANDO BANKSEL NECESSARIO UTILIZAR REGISTRADORES EM BANCO 2 E 3
 MOVWF EEADR			; RECUPERA O ENDEREÇO DA EPPROM PARA LEITURA 
 BANKSEL EECON1			; MUDA PARA BANCO 3
 BSF EECON1,RD			; FORÇA 1 NO BIT DE VERIFICAÇÃO DE LEITURA CONCLUIDA
 BTFSC EECON1,RD		;VERIFICA SE A LEITURA FOI CONCLUIDA
 GOTO $-1	
 BANKSEL EEDATA			; MUDA PARA BANCO 2
 MOVF EEDATA,W			; GUARDA O VALOR LIDO NO REGISTRADOR W 
 BANKSEL ADCON0			; MUDA PARA O BANCO 0
 RETURN
;***********************************************************************************
;LEITURA EEPROM
;***********************************************************************************
;***********************************************************************************
;ESCRITURA EEPROM
;***********************************************************************************
ESCREPROM	
 BCF INTCON,GIE			; DESATIVA INTERRUPÇÃO GERAL
 BANKSEL EEADR			; MUDA PARA O BANCO 2
 MOVWF EEADR			; RECUPERA O ENDEREÇO DA EPPROM PARA ESCRITA
 BANKSEL ADCON0			; MUDA PARA O BANCO 0
 MOVFW  TMP				; RECUPERA DADO A SER ESCRITO
 BANKSEL EEADR			; MUDA PAR AO BANCO 2
 MOVWF EEDATA			; CARREGA O VALOR A SER ESCRITO
 BANKSEL EECON1			; MUDA PARA O BANCO 3
 BCF EECON1, EEIF
 BSF EECON1, WREN 		; HABILITA ESCRITA
 MOVLW 0x55				; CARREGAMENTO OBRIGATORIO NO REGISTRAO EECON2
 BANKSEL EECON2			; NO VALOR DE 0X55 W 0XAA PARA POSSIBILITAR A ESCRITA			
 MOVWF EECON2			;
 MOVLW 0xAA	
 MOVWF EECON2	
 BSF EECON1, WR			; FORÇA O BIT DE ESCRITA QUE SERA LIMPO PELO SISTEMA
CONFESCRITA	
 BTFSC EECON1,WR 		;VERIFICA SE A ESCRITA FOI FINALIZADA, ESCRITA OK?
 GOTO CONFESCRITA	
 MOVLW .80
 CALL DELAY3 ;DELAY DE 1ms
 BCF EECON1,WREN 		; DESABILITA ESCRITA
 BANKSEL ADCON0			; MUDA PARA O BANCO 0
 BSF INTCON,GIE
 RETURN
;***********************************************************************************
;ESCRITURA EEPROM
;***********************************************************************************
;***********************************************************************************
;LEITURA SENHA EEPROM
;***********************************************************************************
LEITURASENHA
 CLRF ADDRESS			; LIMPA REGISTRADOR QUE GUARDA O ENDEREÇO DA EEPROM A SER LIDO
 CLRF ES0				; LIMPA O REGISTRADO QUE GUARDA O BYTE DA SENHA
 MOVLW 0X00				; ENDEREÇO INCIIAL DE LEITURA
 CALL LEEPROM			; EXECUTA A ROTINA DE LEITURA
 MOVWF ES0				; GUARDA O VALOR NO REGISTRADOR DE PRIMEIRO DIGITO DE SENHA 
 INCF ADDRESS,F			; INCREMENTA 1 NO ENDEREÇO DE LEITURA
 ;
 CLRF ES1
 MOVFW ADDRESS
 CALL LEEPROM
 MOVWF ES1
 INCF ADDRESS,F
 ; 
 CLRF ES2
 MOVFW ADDRESS
 CALL LEEPROM
 MOVWF ES2
 INCF ADDRESS,F
 ; 
 CLRF ES3
 MOVFW ADDRESS
 CALL LEEPROM
 MOVWF ES3
 INCF ADDRESS,F
 ;
 CLRF AUXNIVELSEG 		; AUXILIAR PARA BIT DE NIVEL DE SEGURANÇA (NECESSARIO PARA NÃO GRAVAR TODA A WORD)
 MOVFW ADDRESS
 CALL LEEPROM
 MOVWF AUXNIVELSEG
 BCF AUXDIG,5
 BTFSC AUXNIVELSEG,5
 BSF AUXDIG,5
 ;
RETURN 
;***********************************************************************************
;LEITURA SENHA EEPROM
;***********************************************************************************
;
;***********************************************************************************
;GRAVAR SENHA EPROM
;***********************************************************************************
GRAVASENHA
 CLRF    ADDRESS	; LIMPA REGISTRADOR QUE GUARDA O ENDEREÇO DA EEPROM A SER LIDO
 MOVFW   S0			; CARREGA O VALOR DIGITADO NO REGISTRADOR W
 MOVWF   TMP		; GUARDA O VALOR DIGITADO NO REGISTRADOR TMP(TEMPORARIO)
 MOVLW   0X00		; ENDEREÇO INCIIAL DE GRAVAÇÃO
 CALL    ESCREPROM	; EXECUTA ROTINA DE GRAVAÇÃO
 INCF    ADDRESS,F	; INCREMENTA 1 NO ENDEREÇO DE GRAVAÇÃO
 ;
 MOVFW   S1
 MOVWF   TMP
 MOVFW   ADDRESS
 CALL    ESCREPROM
 INCF    ADDRESS,F
 ;
 MOVFW   S2
 MOVWF   TMP
 MOVFW   ADDRESS
 CALL    ESCREPROM
 INCF    ADDRESS,F 
 ;
 MOVFW   S3
 MOVWF   TMP
 MOVFW   ADDRESS
 CALL    ESCREPROM
 INCF    ADDRESS,F 
 ;
 CLRF AUXNIVELSEG  	; AUXILIAR PARA BIT DE NIVEL DE SEGURANÇA (NECESSARIO PARA NÃO GRAVAR TODA A WORD)
 BCF AUXNIVELSEG,5
 BTFSC AUXDIG,5
 BSF AUXNIVELSEG,5
 MOVFW AUXNIVELSEG
 MOVWF   TMP
 MOVFW   ADDRESS
 CALL    ESCREPROM
 ;
 RETURN
;***********************************************************************************
;GRAVAR SENHA EPROM
;***********************************************************************************
;
;***********************************************************************************
;SUBROTINA RFID
;***********************************************************************************
;***********************************************************************************
;LEITURA RFID EEPROM
;*********************************************************************************** 
LEITURARFID
 CLRF ADDRESS			; LIMPA REGISTRADOR QUE GUARDA O ENDEREÇO DA EEPROM A SER LIDO(ENDEREÇO INICIAL 0X00)
 CLRF ERF0				; LIMPA O REGISTRADO QUE GUARDA O BYTE DO RFID
 MOVLW 0X05				; ENDEREÇO INCIIAL DE LEITURA
 MOVWF ADDRESS
 CALL LEEPROM			; EXECUTA A ROTINA DE LEITURA
 MOVWF ERF0				; GUARDA O VALOR NO REGISTRADOR DE PRIMEIRO DIGITO DE RFID
 INCF ADDRESS,F			; INCREMENTA 1 NO ENDEREÇO DE LEITURA
 ;
 MOVLW .80 
 CALL DELAY3
 ;
 CLRF ERF1
 MOVFW ADDRESS
 CALL LEEPROM
 MOVWF ERF1
 INCF ADDRESS,F
 ;
 MOVLW .80 
 CALL DELAY3
 ;
 CLRF ERF2
 MOVFW ADDRESS
 CALL LEEPROM
 MOVWF ERF2
 INCF ADDRESS,F
 ;
 MOVLW .80 
 CALL DELAY3
 ;
 CLRF ERF3
 MOVFW ADDRESS
 CALL LEEPROM
 MOVWF ERF3
 INCF ADDRESS,F
 ;
 MOVLW .80 
 CALL DELAY3
 ;
 CLRF ERF4
 MOVFW ADDRESS
 CALL LEEPROM
 MOVWF ERF4
 INCF ADDRESS,F
 ;
 MOVLW .80 
 CALL DELAY3
 ;
 CLRF ERF5
 MOVFW ADDRESS
 CALL LEEPROM
 MOVWF ERF5
 INCF ADDRESS,F
 ;
 MOVLW .80 
 CALL DELAY3
 ;
 CLRF ERF6
 MOVFW ADDRESS
 CALL LEEPROM
 MOVWF ERF6
 INCF ADDRESS,F
 ;
 MOVLW .80 
 CALL DELAY3
 ;
 CLRF ERF7
 MOVFW ADDRESS
 CALL LEEPROM
 MOVWF ERF7
 INCF ADDRESS,F
 ;
 MOVLW .80 
 CALL DELAY3
 ;
 CLRF ERF8
 MOVFW ADDRESS
 CALL LEEPROM
 MOVWF ERF8
 INCF ADDRESS,F
 ;
 MOVLW .80 
 CALL DELAY3
 ;
 CLRF ERF9
 MOVFW ADDRESS
 CALL LEEPROM
 MOVWF ERF9
 ;
RETURN 
;***********************************************************************************
;LEITURA RFID EEPROM
;***********************************************************************************
;GRAVAR RFID
;***********************************************************************************
GRAVARFID
 MOVLW 0X05				; ENDEREÇO INICIAL DE GRAVAÇÃO DO RFID
 MOVWF  ADDRESS			; GUARDA O ENDEREÇO INCIIAL DE GRAVAÇÃO
 MOVFW   RF0			; MOVE O BYTE DO RFID NO REGISTRADOR W
 MOVWF   TMP			; GUARDA O BYTE NO REGISTRADO TMP (TEMPORARIO)
 MOVFW   ADDRESS		; CARREGA O VALOR DO PRIMEIRO ENDEREÇO NO REGISTRADOR W
 CALL    ESCREPROM		; EXECUTA ROTINA DE ESCRITA
 INCF    ADDRESS,F		; INCREMENTA 1 NO ENDEREÇO DA EEPROM
 ;
 MOVLW .80 
 CALL DELAY3
 ;
 MOVFW   RF1
 MOVWF   TMP
 MOVFW   ADDRESS
 CALL    ESCREPROM
 INCF    ADDRESS,F
 ;
 MOVLW .80 
 CALL DELAY3
 ; 
 MOVFW   RF2
 MOVWF   TMP
 MOVFW   ADDRESS
 CALL    ESCREPROM
 INCF    ADDRESS,F
 ;
 MOVLW .80 
 CALL DELAY3
 ;
 MOVFW   RF3
 MOVWF   TMP
 MOVFW   ADDRESS
 CALL    ESCREPROM
 INCF    ADDRESS,F
 ;
 MOVLW .80  
 CALL DELAY3
 ;
 MOVFW   RF4
 MOVWF   TMP
 MOVFW   ADDRESS
 CALL    ESCREPROM
 INCF    ADDRESS,F
 ;
 MOVLW .80 
 CALL DELAY3
 ;
 MOVFW   RF5
 MOVWF   TMP
 MOVFW   ADDRESS
 CALL    ESCREPROM
 INCF    ADDRESS,F
 ;
 MOVLW .80 
 CALL DELAY3
 ;
 MOVFW   RF6
 MOVWF   TMP
 MOVFW   ADDRESS
 CALL    ESCREPROM
 INCF    ADDRESS,F
 ;
 MOVLW .80 
 CALL DELAY3
 ;
 MOVFW   RF7
 MOVWF   TMP
 MOVFW   ADDRESS
 CALL    ESCREPROM
 INCF    ADDRESS,F
 ;
 MOVLW .80 
 CALL DELAY3
 ;
 MOVFW   RF8
 MOVWF   TMP
 MOVFW   ADDRESS
 CALL    ESCREPROM
 INCF    ADDRESS,F
 ;
 MOVLW .80 
 CALL DELAY3
 ;
 MOVFW   RF9
 MOVWF   TMP
 MOVFW   ADDRESS
 ;
 MOVLW .80 
 CALL DELAY3
 ;
 CALL    ESCREPROM
 GOTO MSGRFID2
;**********************************************************************************
;GRAVAR RFID
;***********************************************************************************
;************************************************************************************
;SUBROTINA RFID
;************************************************************************************
;***********************************************************************************
;SUBROTINA EEPROM
;***********************************************************************************
;
;************************************************************************************
;SUBROTINA TECLADO
;************************************************************************************
;***********************************************************************************
;RECEBE TECLADO VIA INTERRUPÇÃO
;***********************************************************************************
VTECLADO 
 BTFSS	INTCON,RBIF ; VERIFICA FLAG INTERRUPCAO
 RETFIE
 GOTO TECLADO
 ;
TECLADO
 MOVLW	B'11110000' ; MOVE O VALOR PARA O REGISTRADO W PARA FAZER A COMPARAÇÃO
 XORWF	PORTB		;EXECUTA A COMPARAÇÃO COM O VALOR DO PORTB
 BTFSC	STATUS, Z 	; VERIFICA DE EXISTE REALMENTE ALGUMA TECLA ACIONA
 GOTO	FIM
 MOVFW	PORTB 
 MOVWF	AUXTC 		; CARREGA VALOR DO PORTB
 BANCO1
 MOVLW	B'00001111'	; REALIZA INVERSÃO PARA VERIFICAR QUAL PINO FOI ALTERADO
 MOVWF	TRISB
 BANCO0
 MOVFW	AUXTC		; CARREGA NA INVERSÃO O VALOR NO PORTB
 MOVWF	PORTB
 ;
LINHA				;VERIFICA QUAL LINHA FOI ACIONADA
 BTFSS	PORTB, RB0
 MOVLW	.0
 BTFSS	PORTB, RB1
 MOVLW	.1
 BTFSS	PORTB, RB2
 MOVLW	.2
 BTFSS	PORTB, RB3
 MOVLW	.3
COLUNA				;VERIFICA QUAL COLUNA FOI ACIONADA
 BTFSS	PORTB, RB4
 IORLW	.0
 BTFSS	PORTB, RB5
 IORLW	.4
 BTFSS	PORTB, RB6
 IORLW	.8
 BTFSS	PORTB, RB7
 IORLW	.12
 MOVWF	AUX1TC
 BANCO1
 MOVLW	B'11110000'	
 MOVWF	TRISB
 BANCO0
 CLRF	PORTB
 MOVFW	AUX1TC
 CALL TABTECLADO	; BUSCA O VALOR NA TABELA DO DIGITO ACONADO
 MOVWF AUX1TC
 CALL GRVSENHADIG  	;GUARDA O VALOR DIGITADO EM VARIAVEIS TEMPORARIAS 
FIM
 CLRF AUX1TC		; LIMPA A VARIAVEL TEMPORARIA DO DIGITO
 BCF INTCON, RBIF	; LIMPA FLAG DE IMTERRUPÇÃO
 RETFIE
 ;
;***********************************************************************************
;RECEBE TECLADO VIA INTERRUPÇÃO
;***********************************************************************************
;
;***********************************************************************************
;GUARDA SENHA EM VARIAVEIS TEMPORARIAS
;***********************************************************************************
GRVSENHADIG
 BTFSC AUXDIG,6
 GOTO INICIO
 BTFSC AUXDIG,2
 GOTO GRAVARNVSENHA
 BTFSC AUXDIG,4
 GOTO GRAVARNIVELSEG
 INCF S0				; INCREMENTA 1 NA VARIAVEL PARA VERIFICA SE JÁ FOI UTILIZADA
 DECFSZ S0				; CASO JÁ FOI UTILIZADA SALTA PARA VEIFICAÇÃO DE OUTRA VARIAVEL
 GOTO $+0XE
 CALL MSGTECLADO
 MOVFW AUX1TC
 XORLW 0X23
 BTFSC STATUS,Z 
 GOTO GRAVARNVSENHA
 MOVFW AUX1TC
 XORLW 0X2A
 BTFSC STATUS,Z 
 GOTO GRAVARNIVELSEG
 BSF AUXDIG,0
 MOVF AUX1TC,W			; CARREGA O VALOR DO DIGITO NO REGISTRADOR W
 MOVWF S0				; CARRREGA O VALOR DO DIGITO NA VERIAVEL TEMPORARIA
 RETURN					; RETORNA PARA AGUARDAR NOVA INTERRUPÇÃO
 INCF S1
 DECFSZ S1
 GOTO $+5
 CALL MSGTECLADO
 MOVF AUX1TC,W
 MOVWF S1
 RETURN
 INCF S2
 DECFSZ S2
 GOTO $+5
 CALL MSGTECLADO
 MOVF AUX1TC,W
 MOVWF S2
 RETURN
 MOVF AUX1TC,W
 MOVWF S3
 CALL MSGTECLADO 
 BSF AUXDIG,1
 ;
 MOVFW ES0
 XORWF S0
 BTFSS STATUS,Z 
 GOTO MSGTECLADO2
 ;
 MOVFW ES1
 XORWF S1
 BTFSS STATUS,Z 
 GOTO MSGTECLADO2
 ;
 MOVFW ES2
 XORWF S2
 BTFSS STATUS,Z 
 GOTO MSGTECLADO2
 ;
 MOVFW ES3
 XORWF S3
 BTFSS STATUS,Z 
 GOTO MSGTECLADO2
 BCF AUXDIG,0
 GOTO MSGTECLADO3 
 RETURN
 ;
;***********************************************************************************
;GUARDA SENHA EM VARIAVEIS TEMPORARIAS
;***********************************************************************************
;***********************************************************************************
;NOVA SENHA 
;***********************************************************************************
GRAVARNVSENHA
 BTFSC AUXDIG,2			; BIT DE CONTROLE DE DIGITAÇÃO DE NOVA SENHA
 GOTO GRAVARNVSENHA1	
 MOVLW .80 
 CALL DELAY3
 MOVLW MSG5
 MOVWF LCDLINHA1
 MOVLW MSG7
 MOVWF LCDLINHA2
 CALL LCD 
 BSF AUXDIG,2
 ;
GRAVARNVSENHA1
 MOVFW AUX1TC			; DISPREZA O DIGITO #
 XORLW 0X23
 BTFSC STATUS,Z 		
 RETURN
 INCF S0				;INCREMENTA 1 NA VARIAVEL PARA VERIFICA SE JÁ FOI UTILIZADA
 DECFSZ S0				; CASO JÁ FOI UTILIZADA SALTA PARA VEIFICAÇÃO DE OUTRA VARIAVEL
 GOTO $+6
 CALL MSGTECLADO		
 BSF AUXDIG,0
 MOVF AUX1TC,W			; CARREGA O VALOR DO DIGITO NO REGISTRADOR W
 MOVWF S0				; CARRREGA O VALOR DO DIGITO NA VERIAVEL TEMPORARIA
 RETURN					; RETORNA PARA AGUARDAR NOVA INTERRUPÇÃO
 INCF S1
 DECFSZ S1
 GOTO $+5
 CALL MSGTECLADO
 MOVF AUX1TC,W
 MOVWF S1
 RETURN
 INCF S2
 DECFSZ S2
 GOTO $+5
 CALL MSGTECLADO
 MOVF AUX1TC,W
 MOVWF S2
 RETURN
 MOVF AUX1TC,W
 MOVWF S3
 CALL MSGTECLADO 
 CALL GRAVASENHA
 GOTO MSGTECLADO4
;***********************************************************************************
;NOVA SENHA 
;***********************************************************************************
;***********************************************************************************
;NIVEL DE SEGURANÇA
;***********************************************************************************
GRAVARNIVELSEG
 BTFSC AUXDIG,4		; BIT DE CONTROLE DE DIGITAÇÃO DE NIVEL DE SEGURANÇA
 GOTO GRAVARNIVELSEG1
 MOVLW .80 
 CALL DELAY3
 MOVLW MSG5
 MOVWF LCDLINHA1
 MOVLW MSG15
 MOVWF LCDLINHA2
 CALL LCD 
 BSF AUXDIG,4
 RETURN
 ;
GRAVARNIVELSEG1
 MOVFW AUX1TC			; VERIFICA O DIGITO 1 NIVEL
 XORLW 0X31
 BTFSC STATUS,Z 		
 BCF AUXDIG,5
;
 MOVFW AUX1TC			; VERIFICA O DIGITO 2
 XORLW 0X32
 BTFSC STATUS,Z 		
 BSF AUXDIG,5
;
 CLRF S0
 CLRF S1
 CLRF S2
 CLRF S3
 BCF AUXDIG,0
 BCF AUXDIG,1
 BCF AUXDIG,6
 BCF AUXDIG,4
 BCF INTCON,GIE
 MOVLW .255
 CALL DELAY3 
 ;
 MOVLW 0X04				; GRAVA NA EPROM NOVO NIVEL DE SEGURANÇA
 MOVWF ADDRESS
 CLRF AUXNIVELSEG		; AUXILIAR PARA BIT DE NIVEL DE SEGURANÇA (NECESSARIO PARA NÃO GRAVAR TODA A WORD)	
 BTFSC AUXDIG,5
 BSF AUXNIVELSEG,5
 MOVFW AUXNIVELSEG
 MOVWF TMP
 MOVFW ADDRESS
 CALL ESCREPROM
 BSF INTCON,GIE
 GOTO INICIO
;***********************************************************************************
;NOVA SENHA 
;***********************************************************************************
;***********************************************************************************
; MENSAGENS INTERATIVAS E COMANDOS EXTERNOS
;***********************************************************************************
MSGTECLADO
 BTFSC AUXDIG,0		
 GOTO MSGTECLADO1
 BCF RS
 MOVLW 0X01
 CALL ESCREVE 
 MOVLW .2
 CALL DELAY3 
 MOVLW .80
 CALL DELAY3 
 MOVLW 0X86 
 CALL ESCREVE
 BSF RS
 BTFSC AUXDIG,1
 GOTO MSGTECLADO2
 ;
MSGTECLADO1
 MOVLW '*'				; MENSAGEM *
 BSF BUZZER				; BIP DE DIGITAÇÃO
 MOVLW .100 
 CALL DELAY3
 BCF BUZZER
 CALL ESCREVE			
 RETURN
 ;
 ; MSG SENHA INCORRETA
MSGTECLADO2
 MOVLW .80 
 CALL DELAY3
 MOVLW MSG10
 MOVWF LCDLINHA1
 MOVLW MSG3
 MOVWF LCDLINHA2
 CALL LCD 
 BSF BUZZER				; BIP DE ERRO
 MOVLW D'20'
 MOVWF LOOPTEMPO 
 CALL DELAY
 BCF BUZZER
 GOTO INICIO
 ;
 ; MENSAGEM DE PORTÃO ABERTO
MSGTECLADO3
 MOVLW .80 
 CALL DELAY3
 MOVLW MSG4 
 MOVWF LCDLINHA1
 MOVLW MSG12
 BTFSC AUXDIG,5
 MOVLW MSG5
 MOVWF LCDLINHA2
 CALL LCD 
 MOVLW D'20'
 MOVWF LOOPTEMPO 
 CALL DELAY
 BTFSC ALARME,0
 GOTO RESETALARME
 BSF AUXDIG,6		; FLAG DE SENHA OK
 BTFSC AUXDIG,7		; FLAG DE RFID OK (ABRE O PORTÃO CASO O RFID JÁ FOI VERIFICADO)
 GOTO ABREPORTAO	
 BTFSC AUXDIG,5		; VERIFICA O CONTROLE DE NIVEL DE SEGURANÇA
 GOTO MSGNIVELSEG2
 GOTO ABREPORTAO
 ;
RESETALARME
 BCF ALARME,0
 GOTO ABREPORTAO
;
MSGNIVELSEG2
 MOVLW .80 
 CALL DELAY3
 MOVLW MSG5
 MOVWF LCDLINHA1
 MOVLW MSG2
 MOVWF LCDLINHA2
 CALL LCD 
 BSF INTCON,GIE
 GOTO LOOP
 ;
 ; MENSAGEM DE GRAVAÇÃO REALIZADA
MSGTECLADO4
 MOVLW .80 
 CALL DELAY3
 MOVLW MSG5
 MOVWF LCDLINHA1
 MOVLW MSG8
 MOVWF LCDLINHA2
 CALL LCD 
 MOVLW D'20'
 MOVWF LOOPTEMPO 
 CALL DELAY
 GOTO INICIO
 ;
; MENSAGEM DE TAG NÃO CADASTRADA
MSGRFID
 MOVLW .80 
 CALL DELAY3
 MOVLW MSG10
 MOVWF LCDLINHA1
 MOVLW MSG9
 MOVWF LCDLINHA2
 CALL LCD 
 BSF BUZZER				; BIP DE ERRO
 MOVLW D'20'
 MOVWF LOOPTEMPO 
 CALL DELAY
 BCF BUZZER				
 GOTO INICIO
 ;
 ; MENSAGEM DE TAG OK
MSGRFID1
 MOVLW .80 
 CALL DELAY3
 MOVLW MSG5
 MOVWF LCDLINHA1
 MOVLW MSG13
 MOVWF LCDLINHA2
 CALL LCD 
 MOVLW D'20'
 MOVWF LOOPTEMPO 
 CALL DELAY
 BSF AUXDIG,7			; FLAG DE RFID OK
 BTFSC AUXDIG,6			; FLAG DE SENHA OK (ABRE O PORTÃO CASO A SENHA JÁ FOI VERIFICADA)
 GOTO ABREPORTAO
 BTFSC AUXDIG,5			; VERIFICA O CONTROLE DE NIVEL DE SEGURANÇA
 GOTO MSGRFID1A
 GOTO ABREPORTAO
 ;
MSGRFID1A
 MOVLW .80 
 CALL DELAY3
 MOVLW MSG5
 MOVWF LCDLINHA1
 MOVLW MSG14
 MOVWF LCDLINHA2
 CALL LCD 
 BSF INTCON,GIE
 GOTO LOOP
 ;
 ; MENSAGEM DE TAG GARAVADA COM SUCESSO
MSGRFID2
 MOVLW .80 
 CALL DELAY3
 MOVLW MSG5
 MOVWF LCDLINHA1
 MOVLW MSG8
 MOVWF LCDLINHA2
 CALL LCD 
 MOVLW D'20'
 MOVWF LOOPTEMPO 
 CALL DELAY
 GOTO INICIO
;***********************************************************************************
; MENSAGENS INTERATIVAS COM O TECLADO
;***********************************************************************************
;************************************************************************************
;SUBROTINA TECLADO
;************************************************************************************
;************************************************************************************
;SUBROTINA CONTROLE DO PORTAO
;************************************************************************************
ABREPORTAO
 BCF AUXDIG,6
 BCF AUXDIG,7
 BSF PORTAO
 MOVLW .80 
 CALL DELAY3
 MOVLW MSG11
 MOVWF LCDLINHA1
 MOVLW MSG12
 MOVWF LCDLINHA2
 CALL LCD 
 CALL BEEP
 BSF BUZZER
 MOVLW D'80'
 MOVWF LOOPTEMPO 
 CALL DELAY
 BCF BUZZER
 BCF PORTAO
 ;
PORTAOABERTO 
 MOVLW .10
 MOVWF CABERTO
 ;
PORTAOABERTO1 
 BSF BUZZER
 MOVLW .80 
 CALL DELAY3
 BCF BUZZER
 MOVLW D'20'
 MOVWF LOOPTEMPO 
 CALL DELAY
 BTFSC SENSOR
 GOTO PORTAABERTASAIR
 DECFSZ CABERTO
 GOTO PORTAOABERTO1
 ;
PORTAOABERTO2
 CALL BEEP
 BSF LED
 BTFSC SENSOR
 GOTO PORTAABERTASAIR
 BCF LED
 GOTO PORTAOABERTO2
 ;
PORTAABERTASAIR
 MOVLW D'100'
 MOVWF LOOPTEMPO 
 CALL DELAY
 GOTO INICIO
 ;
PORTAOABERTO3
 MOVLW .80 
 CALL DELAY3
 MOVLW MSG5
 MOVWF LCDLINHA1
 MOVLW MSG12
 MOVWF LCDLINHA2
 CALL LCD
 BSF ALARME,0 
 GOTO PORTAOABERTO3A
 ;
PORTAOABERTO3A
 CALL BEEP
 BSF LED
 BTFSS ALARME,0
 GOTO INICIO
 BCF LED
 GOTO PORTAOABERTO3A
 ;
 ;
BEEP
 MOVLW .5
 MOVWF CONTBEEP 
BEEP1
 BSF BUZZER
 MOVLW .80 
 CALL DELAY3
 BCF BUZZER
 MOVLW .80 
 CALL DELAY3
 DECFSZ CONTBEEP
 GOTO BEEP1
 RETURN 
;************************************************************************************
;SUBROTINA CONTROLE DO PORTAO
;************************************************************************************
;************************************************************************************
;SUBROTINA AUXILIARES
;************************************************************************************
ZERAVARIAVEIS
 ; ZERA PORTAS DE I/O E VARIVEIS
 CLRF PORTA
 CLRF PORTB
 CLRF PORTC
 CLRF PORTD
 CLRF PORTE
 CLRF AUXTC
 CLRF AUX1TC
 CLRF S0
 CLRF S1
 CLRF S2
 CLRF S3
 CLRF RF0
 CLRF RF1
 CLRF RF2
 CLRF RF3
 CLRF RF4
 CLRF RF5
 CLRF RF6
 CLRF RF7
 CLRF RF8
 CLRF RF9
 RETURN
 ;
CONFIGURACAO		; CONFIGURAÇÕES INICIAIS
 MOVLW B'00000111'
 MOVWF CMCON 		;DESATIVA MODO DO COMPARADOR ANALOGICO
 BANCO1 			;SELECIONA BANCO 1 DA RAM
 MOVLW B'11111111'	
 MOVWF ADCON1 		;UTILIZANDO PORTA COMO E/S DIGITAL	
 MOVLW B'00000000'
 MOVWF TRISA 		;CONFIGURA I/O DO PORTA
 MOVLW B'11110000'
 MOVWF TRISB 		;CONFIGURA I/O DO PORTB
 MOVLW B'10001010'
 MOVWF TRISC 		;CONFIGURA I/O DO PORTC
 MOVLW B'00000000'
 MOVWF TRISD 		;CONFIGURA I/O DO PORTD
 MOVLW B'00000000'
 MOVWF TRISE 		;CONFIGURA I/O DO PORTE
 MOVLW B'00000000' 
 MOVWF OPTION_REG	;HABILITANDO O PULL-UP PORTB
 ;
 ; DEFINIÇÃO DA COMUNICAÇÃO UART
 MOVLW 0x81        ;0X81 = 9600BPS
 MOVWF SPBRG       ;COFIGURA BAUD RATE  
 ;BSF PIE1,RCIE	   ; HABILITA INTERRUPÇÃO RECEBIMENTO UART
 MOVLW B'00100100'
 MOVWF TXSTA       ;HABILITA TRANSMISSAO ASSINCRONA E MODULO HIGH DE TRANSMISSÃO(MENOR PORCENTAGEM DE ERRO DO CALCULO DE FHZ OSC vs BAUD RATE)
 BANCO0 
 MOVLW B'10001000' 
 MOVWF INTCON 	   ;HABILITADA INTERRUPCOES MUDANÇA DE ESTADO PORTB (BIT3) ; HABILITA INTERRUPÇÕES EXTERNAS (BIT6)
 MOVLW B'10010000' ;HABILITA RECEPCAO ASSINCRONA
 MOVWF RCSTA
 RETURN
;************************************************************************************
;SUBROTINA AUXILIARES
;************************************************************************************
 ; FIM DO PROGRAMA
 END
